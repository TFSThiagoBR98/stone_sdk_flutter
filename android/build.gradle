group 'br.com.stone.sdk_flutter'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.7.21'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    ext.token = rootProject.ext.has('packageCloudToken') ? rootProject.ext.get('packageCloudToken') : (
        project.properties.containsKey('StonePos_packageCloudToken') ?
        project.properties['StonePos_packageCloudToken'] : System.getenv('StonePos_packageCloudToken')
    )

    if (token == null || token.isEmpty()) {
        throw new RuntimeException("You need to set the StonePos_packageCloudToken either in gradle.properties or in your env")
    }

    repositories {
        google()
        mavenCentral()
        maven { url "https://packagecloud.io/priv/${token}/stone/pos-android/maven2" }
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://packagecloud.io/priv/${token}/stone/pos-android/maven2" }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
    }

    configurations.implementation {
        // Conflicts with net.sf.kxml:kxml2:2.3.0 from integration_test flutter
        exclude group: "xpp3", module: "xpp3_min"
    }

    configurations.debugImplementation {
        // Conflicts with net.sf.kxml:kxml2:2.3.0 from integration_test flutter
        exclude group: "xpp3", module: "xpp3_min"
    }


    def stone_sdk_version = "4.4.0"

    String posMode = rootProject.ext.has('posMode') ? rootProject.ext.get('posMode') : (
        project.properties.containsKey('StonePos_posMode') ?
            project.properties['StonePos_posMode'] : (System.getenv('StonePos_posMode') ?  System.getenv('StonePos_posMode') : 'pos')
    )

    String includeIngenico = rootProject.ext.has('includeIngenico') ? rootProject.ext.get('includeIngenico') : (
        project.properties.containsKey('StonePos_includeIngenico') ?
            project.properties['StonePos_includeIngenico'] : (System.getenv('StonePos_includeIngenico') ? System.getenv('StonePos_includeIngenico') : 'true')
    )

    String includeSunmi = rootProject.ext.has('includeSunmi') ? rootProject.ext.get('includeSunmi') : (
        project.properties.containsKey('StonePos_includeSunmi') ?
            project.properties['StonePos_includeSunmi'] : (System.getenv('StonePos_includeSunmi') ? System.getenv('StonePos_includeSunmi') : 'true')
    )

    String includeGertec = rootProject.ext.has('includeGertec') ? rootProject.ext.get('includeGertec') : (
        project.properties.containsKey('StonePos_includeGertec') ?
            project.properties['StonePos_includeGertec'] : (System.getenv('StonePos_includeGertec') ? System.getenv('StonePos_includeGertec') : 'true')
    )

    if (posMode.isEmpty()) {
        throw new RuntimeException("You need to set the StonePos_posMode either in gradle.properties or in your env")
    }

    if ( posMode.equalsIgnoreCase('pos') ) {
        if (includeIngenico.equalsIgnoreCase('true')) {
            dependencies {
                implementation "br.com.stone:stone-sdk-posandroid-ingenico:$stone_sdk_version"
            }
        }
        if (includeSunmi.equalsIgnoreCase('true')) {
            dependencies {
                implementation "br.com.stone:stone-sdk-posandroid-sunmi:$stone_sdk_version"
            }
        }
        if (includeGertec.equalsIgnoreCase('true')) {
            dependencies {
                implementation "br.com.stone:stone-sdk-posandroid-gertec:$stone_sdk_version"
            }
        }
    }

    dependencies {
        implementation 'net.sf.kxml:kxml2:2.3.0'
        implementation "br.com.stone:stone-sdk:$stone_sdk_version"
        debugImplementation "br.com.stone.sdk.android:envconfig:$stone_sdk_version"
        implementation "br.com.stone:stone-sdk-posandroid:$stone_sdk_version"
        implementation (group: "androidx.lifecycle", name: "lifecycle-viewmodel-ktx") {
            // Conflicts with firebase dep tree: firebase_core
            version {
                strictly "[2.5.1]"
            }
        }
        implementation 'com.izettle:html2bitmap:1.9'
    }
}
