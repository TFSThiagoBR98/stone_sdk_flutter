group 'br.com.stone.sdk_flutter'
version '0.1-SNAPSHOT'

buildscript {
    ext {
        kotlin_version = '1.7.21'        
        stone_sdk_version = '4.8.2'

        localProp = new Properties()
        fileName = 'local.properties'
        if (project.rootProject.file(fileName).exists()) {
            localProp.load(new FileInputStream(rootProject.file(fileName)))
        }
        packageCloudReadToken = System.env.PACKAGECLOUD_READ_TOKEN != null ? System.env.PACKAGECLOUD_READ_TOKEN : localProp["packageCloudReadToken"] ?: ""

        if (packageCloudReadToken == null || packageCloudReadToken.isEmpty()) {
            throw new RuntimeException("You need to set the packageCloudReadToken either in local.properties or in your env")
        }
    }
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()

        maven { url "https://packagecloud.io/priv/${packageCloudReadToken}/stone/pos-android/maven2" }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 22
    }

    configurations.all {
        // Conflicts with net.sf.kxml:kxml2:2.3.0 from integration_test flutter
        exclude group: "xpp3", module: "xpp3_min"
    }

    dependencies {
        implementation "br.com.stone:stone-sdk:$stone_sdk_version"
        implementation "br.com.stone:stone-sdk-posandroid:$stone_sdk_version"
        implementation "br.com.stone:stone-sdk-posandroid-sunmi:$stone_sdk_version"
        implementation "br.com.stone:stone-sdk-posandroid-ingenico:$stone_sdk_version"
        implementation "br.com.stone:stone-sdk-posandroid-gertec:$stone_sdk_version"
        implementation "br.com.stone:stone-sdk-posandroid-positivo:$stone_sdk_version"

        // Avoid package conflict with integration_test
        implementation 'net.sf.kxml:kxml2:2.3.0'
        implementation (group: "androidx.lifecycle", name: "lifecycle-viewmodel-ktx") {
            // Conflicts with firebase dep tree: firebase_core
            version {
                strictly "[2.5.1]"
            }
        }
    }
}
